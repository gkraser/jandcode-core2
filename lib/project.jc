import jandcode.commons.*
import jandcode.jc.*
import jandcode.jc.std.*
import jandcode.jc.std.gradle.*

class P extends ProjectScript {

    void onInclude() {
        project.name = "lib-builder"
        //
        download_ojdbc8()
        //
        include(GradleTools).with {
            publishDir = "../_jc/${project.name}"
            tempDir = "../temp/${project.name}"

            String jcLibsFile = wd('../jc-libs.txt')
            Set jcLibs = new HashSet(new File(jcLibsFile).readLines())

            // библиотеки, от которых зависит jc берем из места,
            // откуда они в classpath появились
            String newJarDir = wd("../_jc/_lib")
            filter { List<GradleLibDef> libs ->
                for (GradleLibDef lib : libs) {
                    if (jcLibs.contains(lib.name)) {
                        String newJar = UtFile.join(newJarDir, "${lib.name}.jar")
                        if (UtFile.exists(newJar)) {
                            lib.jar = newJar
                        } else {
                            log.warn("Не найдена библиотека ${lib.name} из зависимостей " +
                                    "jandcode-jc ${jcLibsFile} в каталоге ${newJarDir}")
                        }
                    }
                }
            }
        }

    }

    void download_ojdbc8() {
        String url = "https://bitbucket.org/gkraser/jandcode-core2/downloads/ojdbc8.jar"
        String fnDest = wd("_oracle/ojdbc8.jar")
        String fnTemp = wd("_oracle/ojdbc8.tmp")
        try {
            if (UtFile.exists(fnDest)) {
                return
            }
            log.info("download: ${url}...")
            ut.cleanfile(fnTemp)
            ant.get(src: url,
                    dest: fnTemp,
                    verbose: true,
            )
            ant.copy(file: fnTemp, tofile: fnDest, overwrite: true)
            ant.delete(file: fnTemp)
        } catch (e) {
            log.warn("Не удалось скачать [${url}] в [${fnDest}]. Предоставьте этот файл самостоятельно")
            throw e
        }
    }

}
