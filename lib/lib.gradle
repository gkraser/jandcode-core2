repositories {
    mavenCentral()
}

configurations {
    implementation1  // для индивидуальных тестов

    implementation {
        // это предоставляет slf4j
        exclude group: "commons-logging"
        // это предоставляет flexmark
        exclude group: "junit"
        exclude group: "log4j"
        exclude group: "org.apache.logging.log4j"
        // это (случайно видимо) тащит vfs
        exclude group: "org.apache.hadoop"
        // это не нужно
        exclude module: "junit-bom"
    }
}

if (tasks.findByName('dependencyUpdates')) {
    tasks.named("dependencyUpdates").configure {
        rejectVersionIf {
            String s = it.candidate.version.toLowerCase()
            if (s.contains('alpha')) return true
            if (s.contains('-m')) return true
            if (s.contains('-beta')) return true
            return false
        }
    }
}

dependencies {
    String v, vp

    // ant
    v = '1.10.12'
    implementation group: 'org.apache.ant', name: 'ant', version: v
    implementation group: 'org.apache.ant', name: 'ant-launcher', version: v
    implementation(group: 'org.apache.ant', name: 'ant-junit', version: v) {
        transitive = false
    }
    implementation(group: 'org.apache.ant', name: 'ant-junitlauncher', version: v) {
        transitive = false
    }

    // junit5
    v = '5.8.2'
    vp = '1.8.2'  // platform
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: v
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: v
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: v
    implementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: vp
    implementation group: 'org.junit.platform', name: 'junit-platform-console', version: vp

    // apiguardian for junit5
    implementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2'

    // apache commons
    implementation group: 'org.apache.commons', name: 'commons-vfs2', version: '2.9.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.21'
    implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.9.0'
    implementation group: 'org.apache.commons', name: 'commons-pool2', version: '2.11.1'
    implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.9.0'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    // groovy
    v = '3.0.9'
    implementation group: 'org.codehaus.groovy', name: 'groovy', version: v
    implementation(group: 'org.codehaus.groovy', name: 'groovy-ant', version: v) {
        exclude group: 'org.apache.ant'
        exclude module: 'groovy-groovydoc'
    }
    implementation group: 'org.codehaus.groovy', name: 'groovy-json', version: v
    implementation group: 'org.codehaus.groovy', name: 'groovy-xml', version: v
    implementation group: 'org.codehaus.groovy', name: 'groovy-templates', version: v
    implementation(group: 'org.codehaus.groovy', name: 'groovy-groovydoc', version: v) {
        exclude module: 'groovy-docgenerator'
    }

    // jansi
    implementation group: 'org.fusesource.jansi', name: 'jansi', version: '2.4.0'

    // picocli, для groovy
    implementation group: 'info.picocli', name: 'picocli', version: '4.6.2'

    // slf4j
    v = '1.7.32'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: v
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: v
    implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: v

    // logback
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.10'

    // gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'

    // jdbc
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.3.1'
    implementation group: 'com.h2database', name: 'h2', version: '2.0.204'
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.4'
    implementation group: 'com.oracle.database.jdbc', name: 'ojdbc8', version: '21.4.0.0.1', transitive: false

    // flexmark
    v = '0.62.2'
    implementation group: 'com.vladsch.flexmark', name: 'flexmark', version: v
    implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-gfm-strikethrough', version: v
    implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-tables', version: v
    implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-yaml-front-matter', version: v
    implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-admonition', version: v
    implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-definition', version: v
    implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-anchorlink', version: v
    implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-attributes', version: v

    // servlet-api
    implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

    // undertow
    implementation(group: 'io.undertow', name: 'undertow-servlet', version: '2.2.14.Final') {
        exclude module: 'jboss-annotations-api_1.2_spec'
        exclude module: 'jboss-servlet-api_4.0_spec'
    }

    // poi
    v = '5.1.0'
    implementation group: 'org.apache.poi', name: 'poi', version: v
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: v
    implementation group: 'org.apache.poi', name: 'poi-scratchpad', version: v

    // lucene
    v = '9.0.0'
    implementation group: 'org.apache.lucene', name: 'lucene-core', version: v
    implementation group: 'org.apache.lucene', name: 'lucene-analysis-common', version: v
    implementation group: 'org.apache.lucene', name: 'lucene-queryparser', version: v

    // javassist
    v = '3.28.0-GA'
    implementation group: 'org.javassist', name: 'javassist', version: v

}

